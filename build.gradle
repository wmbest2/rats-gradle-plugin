apply plugin: 'groovy'
apply plugin: 'nexus'

group = 'com.wmbest.gradle'
version = '0.2.0'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:0.10.+'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.apache.httpcomponents:httpmime:4.2.5'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Remote Android Test Service Gradle plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty("user.name"),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty("java.version")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

modifyPom {
    project {
        artifactId 'rats'
        name 'rats'
        description 'Gradle Plugin for Remote Android Test Service Testing'
        url 'https://github.com/wmbest2/rats-gradle-plugin'

        scm {
            url 'https://github.com/wmbest2/rats-gradle-plugin'
            connection 'scm:git@github.com:wmbest2/rats-gradle-plugin.git'
            developerConnection 'scm:git@github.com:wmbest2/rats-gradle-plugin.git'
        }

        licenses {
            license {
                name 'MIT'
                url 'http://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'wmbest2'
                name 'William Best'
                email 'wmbest2@gmail.com'
            }
        }
    }
}
